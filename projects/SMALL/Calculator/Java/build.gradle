task wrapper(type: Wrapper) {
  gradleVersion = '2.2.1'
}


// This is all you need for a basic Java project. Everything else after this
// one line is auxiliary.
apply plugin: 'java'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'
sourceSets {
    main {
        java {
            srcDir './'
        }
    }
}

task sourceJar(type: Jar) {
  description = 'Builds a source JAR artifact suitable for Maven deployment'
  classifier = 'sources'
  from sourceSets.main.java
}

def nativeHeadersDir = file("$buildDir") // Keeping it simple

task nativeHeaders {
  def outputFile = file("$nativeHeadersDir/native.h")
  inputs.files sourceSets.main.output
  outputs.file outputFile
  doLast {
    outputFile.parentFile.mkdirs()
    exec {
      executable org.gradle.internal.jvm.Jvm.current().getExecutable('javah')
        args '-o', outputFile
        args '-classpath', sourceSets.main.output.classesDir
        args 'Calculator'
    }
  }
}

build.dependsOn sourceJar, nativeHeaders

artifacts {
  archives sourceJar//, javadocJar
}

apply plugin: 'eclipse' // Provides tasks for creating an Eclipse project
apply plugin: 'idea'    // '' for creating an IntelliJ project

eclipse {
  // Separate Eclipse's build output from Gradle's
  // http://forums.gradle.org/gradle/topics/eclipse_generated_files_should_be_put_in_the_same_place_as_the_gradle_generated_files
  classpath {
    defaultOutputDir = file("$buildDir/eclipse-classes")
  }
}

apply plugin: 'maven'





